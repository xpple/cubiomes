import dev.xpple.cubiomes.buildscript.CreateJavaBindingsTask

import java.time.Instant
import java.time.ZoneOffset

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'dev.lukebemish.central-portal-publishing' version "${central_portal_publishing_version}"
}

static def createPreReleaseVersion() {
    def now = Instant.now().atZone(ZoneOffset.UTC)
    def year = now.year
    def month = now.monthValue
    def day = now.dayOfMonth
    def epochSeconds = now.toEpochSecond()

    def commitHash = System.getenv("GITHUB_SHA")
    def metaData = commitHash ? "+${commitHash}" : ""

    return "${year}.${month}.${day}.${epochSeconds}${metaData}"
}

version = "${project.base_version}-${createPreReleaseVersion()}"
group = project.maven_group
description = project.description

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:${project.junit_version}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

clean {
    delete fileTree("${projectDir}/src/main/java/dev/xpple/cubiomes") {
        exclude 'CubiomesInit.java'
    }
}

tasks.register('createJavaBindings', CreateJavaBindingsTask) {}
compileJava.dependsOn('createJavaBindings')

createJavaBindings.dependsOn(clean)

processResources {
    from("LICENSE")

    from("LICENSE_cubiomes.txt")

    from("LICENSE_loot_library.h.txt")
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 23
}

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23

    withSourcesJar()
    withJavadocJar()
}

javadoc {
	options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

centralPortalPublishing.bundle('cubiomes') {
    username = System.getenv("PORTAL_USER")
    password = System.getenv("PORTAL_TOKEN")

    publishingType = "USER_MANAGED"
    // publishingType = "AUTOMATIC"
}

tasks.publish.dependsOn tasks.named("publishCubiomesCentralPortalBundle")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()
            from components.java

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/xpple/cubiomes/tree/java-bindings'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'xpple'
                        name = 'Frederik van der Els'
                        email = 'fred@xpple.dev'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/xpple/cubiomes.git'
                    developerConnection = 'scm:git:ssh://github.com/xpple/cubiomes.git'
                    url = 'https://github.com/xpple/cubiomes'
                    tag = 'java-bindings'
                }
            }
        }
    }

    repositories {
        centralPortalPublishing.portalBundle(':', 'cubiomes')
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/xpple/cubiomes"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "xpple"
            url = "sftp://xpple.dev:22/maven.xpple.dev/httpdocs/maven2"
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
        }
    }
}

signing {
    // only register task on GitHub Actions
    if (System.getenv("GITHUB_ACTIONS") != "true") {
        logger.lifecycle("Disabled artifact signing!")
        return
    }
    def keyData = [
        signingKeyId: findProperty("signingKeyId"),
        signingKey: findProperty("signingKey"),
        signingPassword: findProperty("signingPassword"),
    ]
    def missing = keyData.findAll { it.value == null }
    if (!missing.isEmpty()) {
        logger.lifecycle("Missing in memory PGP key (missing ${missing.keySet().join(", ")}), disabled artifact signing!")
        return
    }
    useInMemoryPgpKeys(keyData.signingKeyId, keyData.signingKey, keyData.signingPassword)
    sign publishing.publications.mavenJava
    logger.lifecycle("Enabled artifact signing!")
}
